plugins {
    id 'java'
    id 'application'
    id "org.sonarqube" version "2.7.1"
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

sourceCompatibility = 1.14
targetCompatibility = 1.14

repositories {
    mavenCentral()
}

javafx {
    version = "14"
    modules = [ 'javafx.controls', 'javafx.base', 'javafx.fxml', 'javafx.graphics', 'javafx.media']
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    // Library for JSON
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'
    // Command line argument parsing
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    // Reflect for loading class in the GUI
    compile "org.reflections:reflections:0.9.11"
    // MongoDB Driver
    compile 'org.mongodb:mongodb-driver:3.4.3'
    // File IO library for copying directories
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    // Library for parsing the java AST
    compile 'com.github.javaparser:javaparser-core:3.5.2'

    // V2 Dependencies
    implementation 'info.picocli:picocli:4.2.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.2'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'org.apache.logging.log4j:log4j-api:2.13.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.13.1'
    implementation 'com.google.dagger:dagger:2.27'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.27'
}

mainClassName = 'chalkbox.api.ChalkBox'

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    baseName = project.name
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

